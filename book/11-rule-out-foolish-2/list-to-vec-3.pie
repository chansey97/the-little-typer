;; Using a separate proof is called extrinsic proof
#lang pie

;; length
(claim step-length
  (Pi ((E U))
    (-> E (List E) Nat
        Nat)))
(define step-length
  (lambda (E)
    (lambda (e es length_es)
      (add1 length_es))))

(claim length
  (Pi ((E U))
    (-> (List E) Nat)))
(define length
  (lambda (E )
    (lambda (es )
      (rec-List es
        0
        (step-length E)))))

;; list->vec
(claim mot-list->vec
  (Pi ((E U))
    (-> (List E) U)))
(define mot-list->vec
  (lambda (E)
    (lambda (es)
      (Vec E (length E es)))))

(claim step-list->vec
  (Pi ((E U)
       (e E)
       (es (List E)))
    (-> (mot-list->vec E es)
        (mot-list->vec E (:: e es)))))
(define step-list->vec
  (lambda (E e es)
    (lambda (list->vec_es)
      (vec:: e list->vec_es))))

(claim list->vec
  (Pi ((E U)
       (es (List E)))
    (Vec E (length E es))))
(define list->vec
  (lambda (E es)
    (ind-List es
      (mot-list->vec E)
      vecnil
      (step-list->vec E))))

;; --- Now use extrinsic proof ---

;; vec->list
(claim mot-vec->list
  (Pi ((E U)
       (l Nat))
    (-> (Vec E l)
        U)))
(define mot-vec->list
  (lambda (E l)
    (lambda (es)
      (List E))))

(claim step-vec->list
  (Pi ((E U)
       (l-1 Nat)
       (e E)
       (es (Vec E l-1)))
    (-> (mot-vec->list E l-1 es)
        (mot-vec->list E (add1 l-1) (vec:: e es)))))
(define step-vec->list
  (lambda (E l-1 e es)
    (lambda (vec->list_es)
      (:: e vec->list_es))))

(claim vec->list
  (Pi ((E U)
       (l Nat))
    (-> (Vec E l) (List E))))
(define vec->list
  (lambda (E l)
    (lambda (es)
      (ind-Vec l es
               (mot-vec->list E)
               nil
               (step-vec->list E)))))

;; list->vec->list=
(claim mot-list->vec->list=
  (Pi ((E U))
    (-> (List E)
      U)))

(define mot-list->vec->list=
  (lambda (E es)
    (= (List E)
      es
      (vec->list E
        (length E es)
        (list->vec E es)))))

(claim ::-fun
  (Pi ((E U))
    (-> E (List E) (List E))))

(define ::-fun
  (lambda (E)
    (lambda (e es)
      (:: e es))))

(claim step-list->vec->list=
  (Pi ((E U)
       (e E)
       (es (List E)))
    (-> (mot-list->vec->list= E
          es)
      (mot-list->vec->list= E
        (:: e es)))))

(define step-list->vec->list=
  (lambda (E e es)
    (lambda (list->vec->list=_es)
      (cong list->vec->list=_es
        (::-fun E e)))))

(claim list->vec->list=
  (Pi ((E U)
       (es (List E)))
    (= (List E)
       es
       (vec->list E (length E es)
         (list->vec E es)))))

(define list->vec->list=
  (lambda (E es)
    (ind-List es (mot-list->vec->list= E)
      (same nil)
      (step-list->vec->list= E))))

;; an extrinsic proof 就像写测试，用证明来程序满足的属性

;; (list->vec Atom
;;  (:: 'bowl-of-porridge
;;    (:: 'banana
;;      (:: 'nuts nil))))

